; Note that this puzzle is "insecure" as it stands
; The person who owns the CASH_OUT_PUZHASH can take out a flash loan to:
; - fill the piggybank
; - receive their funds
; - return the flash loan
;
; This effectively lets someone access the money whenever they want if they have access to a flash loan
; keybase://chat/chia_network.public#chialisp/4465
; https://www.youtube.com/watch?v=JU26LlHAKWU&t=291s
; https://chialisp.com/docs/security#the-flash-loan-from-god-attack
(mod (
        TARGET_AMOUNT
        CASH_OUT_PUZZLE_HASH
        my_amount
        new_amount
        my_puzzlehash
     )

     (include condition_codes.clib)

     (defun-inline cash_out (CASH_OUT_PUZZLE_HASH my_amount new_amount my_puzzlehash)
      (list
        (list CREATE_COIN CASH_OUT_PUZZLE_HASH new_amount)
        (list CREATE_COIN my_puzzlehash 0)
        (list ASSERT_MY_AMOUNT my_amount)
        (list ASSERT_MY_PUZZLEHASH my_puzzlehash)
        (list CREATE_COIN_ANNOUNCEMENT new_amount)
      )
     )

     (defun-inline recreate_self (my_amount new_amount my_puzzlehash)
      (list
        (list CREATE_COIN my_puzzlehash new_amount)
        (list ASSERT_MY_AMOUNT my_amount)
        (list ASSERT_MY_PUZZLEHASH my_puzzlehash)
        (list CREATE_COIN_ANNOUNCEMENT new_amount)
      )
     )

     ; main
     (if (> new_amount my_amount)
       (if (> new_amount TARGET_AMOUNT)
        (cash_out CASH_OUT_PUZZLE_HASH my_amount new_amount my_puzzlehash)
        (recreate_self my_amount new_amount my_puzzlehash)
       )
       (x)
     )

)